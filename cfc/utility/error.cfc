<cfcomponent>
	<!---*****************************************************--->
	<!---init   --->
	<!---Psuedo constructor that allows us to play our object games. --->
	<!---*****************************************************--->
	<cffunction name="init" output="FALSE" access="public"  returntype="any" hint="Psuedo constructor that allows us to play our object games." >
		<cfargument name="debug" required="TRUE" hint="The debug setting bean." />
		<cfargument name="settings" required="TRUE" hint="The debug setting bean." />
		<cfset variables.debug = arguments.debug />
		<cfset variables.settings = arguments.settings />
		<cfreturn This />
	</cffunction>

	<!--- *********************************************************** --->
	<!--- generateErrorDebugging                                      --->
	<!--- Generates the full debugging of a application error.        --->
	<!--- *********************************************************** --->
	<cffunction access="public" name="generateErrorDebugging" output="false" returntype="string" hint="Generates the full debugging of a application error.">
		<cfargument name="exception" required="TRUE" hint="The exception object generated by the error." />
		<cfargument name="cgi" default="#structNew()#" hint="The cgi scope present during the error." />
		<cfargument name="application" default="#structNew()#" hint="The application scope present during the error." />
		<cfargument name="session" default="#structNew()#" hint="The session scope present during the error.." />
		<cfargument name="url" default="#structNew()#" hint="The url scope present during the error." />
		<cfargument name="form" default="#structNew()#" hint="The form scope present during the error." />
		
		<cfset var results= "" />

		<cfsavecontent variable="results">
			<cfoutput>
			<h1>Error Message</h1>
			<cfif isdefined("arguments.exception")>
				<table>
				<tr><td valign="top"><strong>Date/Time:</strong></td><td>#Now()#</td></tr>
				<tr><td valign="top"><strong>Error:</strong></td><td>#arguments.Exception.message#</td></tr>
				<tr><td valign="top"><strong>Detail:</strong></td><td>#arguments.Exception.Detail#</td></tr>
				<tr><td valign="top"><strong>Page:</strong></td><td>#arguments.cgi.script_name#</td></tr>
				</table>
				<br><br>
			</cfif>
			</cfoutput>

		<!--- Optional:  this code dumps the session structure of the user who encountered the error to assist you in your bug hunt. --->
		<cfif variables.debug.getException()>
			<h1>Exception</h1>
			<cfdump var="#arguments.exception#" label="Exception" />
		</cfif>

		<cfif variables.debug.getApplication()>
			<h1>Application</h1>
			<cfdump var="#arguments.application#"  label="Application" />
		</cfif>

		<cfif variables.debug.getSession()>
			<h1>Session</h1>
			<cfdump var="#arguments.session#"  label="Session" />
		</cfif>

		<cfif variables.debug.getCGI()>
			<h1>CGI</h1>
			<cfdump var="#arguments.cgi#"  label="CGI" />
		</cfif>

		<cfif variables.debug.getUrl()>
			<h1>URL</h1>
			<cfdump var="#arguments.url#"  label="Url" />
		</cfif>

		<cfif variables.debug.getServer()>
			<h1>Server</h1>
			<cfdump var="#arguments.server#"  label="Form" />
		</cfif>

		<cfif variables.debug.getForm()>
			<h1>Form</h1>
			<!--- Please take care to not reveal password information --->
			<!--- Do not allow cfmail to send out passwords.  --->
			<cfif isdefined("arguments.form.password")>
				<cfset arguments.form.password="*****************" />
			</cfif>

			<cfdump var="#arguments.form#" />
		</cfif>
		
		</cfsavecontent>


		<cfreturn results />
	</cffunction>
	
	<!--- *********************************************************** --->
	<!--- email                                                --->
	<!--- Emails the administrator                                    --->
	<!--- *********************************************************** --->
	<cffunction name="email" output="false" returntype="void" hint="Creates an email message based on an error, and sends it to the input[email]">
		<cfargument name="contents" required="yes" hint="The contents of a error report." />

		<cfmail type="html" to="#variables.settings.GetAdminEmail()#" from="#variables.settings.GetAdminEmail()#" subject="Error - #variables.settings.GetTitle()#">
			#contents#
		</cfmail>

	</cffunction>



</cfcomponent>